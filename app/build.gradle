apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId "razerdp.friendcircle"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        if (isModule.toBoolean()) {
            buildConfigField 'boolean', 'isModule', 'true'
        } else {
            buildConfigField 'boolean', 'isModule', 'false'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res/layouts/activity',
                            'src/main/res/layouts/momentsview',
                            'src/main/res/layouts/popup',
                            'src/main/res/layouts/widget',
                            'src/main/res/layouts',
                            'src/main/res'
                    ]
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'org.greenrobot:eventbus:3.0.0'
    //朋友圈图片控件
    compile 'com.github.razerdp:PhotoContents:v1.4.2'
    //图片选择模块尚未完成，所以暂时不需要分开依赖
    if (!isModule.toBoolean()) {
        compile project(':baselib')
        compile project(':baseuilib')
        compile project(path: ':network')
        compile project(':photoselect')
        annotationProcessor rootProject.ext.dependencies.arouter_compiler


    } else {
        compile project(':baselib')
        compile project(':baseuilib')
        compile project(path: ':network')
    }

}
